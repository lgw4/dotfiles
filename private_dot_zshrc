#!/usr/bin/env zsh
fpath+=(
    "$HOME/.local/share/zsh/local-functions"
)
if [[ -v HOMEBREW_ROOT ]]; then
    fpath=("$HOMEBREW_ROOT/share/zsh/site-functions" $fpath)
fi
typeset -U fpath
autoload -Uz compinit && compinit
autoload -Uz bashcompinit && bashcompinit

for f in "$HOME"/.zshrc.d/**/*.zsh(N); do
    if [[ -r $f ]]; then
        source $f
    fi
done

if [[ -d "$HOMEBREW_ROOT" ]] && (( $+commands[brew] )); then
    export HOMEBREW_CASK_OPTS="--appdir=~/Applications"
fi

complete -o nospace -C "$GOPATH"/bin/gocomplete go

if (( $+commands[starship] )); then
    eval "$(starship init zsh)"
else
    PROMPT=$'\n%F{red}%n%f at %F{magenta}%m%f in %F{blue}%1~%f\n%(!.#.$) '
fi

precmd_functions+=(set_window_title)

if (( $+commands[direnv] )); then
    eval "$(direnv hook zsh)"
fi

if [[ -e "${HOME}/.iterm2_shell_integration.zsh" ]]; then
    source "${HOME}/.iterm2_shell_integration.zsh"
fi
